import QtQuick
import QtQuick.Window
import QtQuick.Controls
import Qt.labs.platform as Platform
import "calendar.mjs" as Lunar
import Clock
import WindowManagerModule

ApplicationWindow {
    id: window
    visible: true
    title: qsTr("æ¨¡æ‹Ÿæ—¶é’Ÿ")
    color: "transparent"

    property bool stayOnTop: WindowManager.stayOnTop
    property int currentSizeIndex: 1
    flags: Qt.FramelessWindowHint

    width: dp(sizeModel.get(currentSizeIndex).value)
    height: width

    // ç»‘å®šå±å¹•ï¼ˆé‡è¦ï¼‰
    Component.onCompleted: {
        Dp.bindToWindow(window)
        // å°†çª—å£ä¼ é€’ç»™ WindowManager
        WindowManager.setWindow(window)
    }
    function dp (x) { return Dp.dp(x) }

    ListModel {
        id: sizeModel
        ListElement {displayName: "ä¸­æ¯"; value: 350}
        ListElement {displayName: "å¤§æ¯"; value: 500}
        ListElement {displayName: "è¶…å¤§æ¯"; value: 700}
    }

    // ç³»ç»Ÿæ‰˜ç›˜å›¾æ ‡
    Platform.SystemTrayIcon {
        id: systemTray
        visible: true
        tooltip: qsTr("æ¨¡æ‹Ÿæ—¶é’Ÿ")
        icon.source: "qrc:/resources/clock-circle.png"

        onActivated: function(reason) {
            if (reason === Platform.SystemTrayIcon.Trigger) {
                if (window.visible && window.active) {
                    window.hide()
                } else {
                    window.show()
                    window.raise()
                    window.requestActivate()
                }
            }
        }

        menu: Platform.Menu {
            Platform.MenuItem {
                text: qsTr("æ˜¾ç¤º/éšè—")
                onTriggered: {
                    if (window.visible) {
                        window.hide()
                    } else {
                        window.show()
                        window.raise()
                        window.requestActivate()
                    }
                }
            }

            Platform.MenuSeparator {}

            Platform.MenuItem {
                text: window.stayOnTop ? qsTr("ðŸ“Œç½®é¡¶") : qsTr("ç½®é¡¶")
                onTriggered: WindowManager.stayOnTop = !WindowManager.stayOnTop
            }

            Platform.MenuSeparator {}

            Platform.MenuItem {
                text: qsTr("é€€å‡º")
                onTriggered: Qt.quit()
            }
        }
    }

    Text {
        text: "ðŸ“Œ"
        anchors.right: parent.right
        anchors.top: window.top
        visible: window.stayOnTop
        font.pixelSize: dp(18)
        anchors.rightMargin: dp(4)
        anchors.topMargin: dp(4)
    }

    MMenu {
        id: contextMenu
        closePolicy: Popup.CloseOnPressOutside | Popup.CloseOnEscape

        Action {
            text: window.stayOnTop ? qsTr("ðŸ“Œç½®é¡¶") : qsTr("ç½®é¡¶")
            onTriggered: WindowManager.stayOnTop = !WindowManager.stayOnTop
        }

        MenuSeparator {}

        Menu {
            title: qsTr("å°ºå¯¸")

            Repeater {
                model: sizeModel
                MenuItem {
                    required property string displayName
                    required property real value
                    required property int index
                    text: `${displayName}\t${value}dp`
                    checkable: true
                    checked: window.currentSizeIndex === index
                    onTriggered: window.currentSizeIndex = index
                }
            }
        }

        MenuSeparator {}

        Action {
            text: qsTr("é€€å‡º(æ—¶é’ŸåŒå‡»)")
            onTriggered: Qt.quit()
        }
    }

    Rectangle {
        id: clock
        anchors.fill: parent
        anchors.margins: dp(10)
        radius: width / 2
        color: "white"
        border.color: "#333"
        border.width: dp(10)
        antialiasing: true

        property real baseHourMarkHeight: clock.height/20
        property real baseHourMarkWidth: clock.height/60
        property real baseHourMarkTopMargin: clock.height/40

        // ä½¿çª—å£å¯æ‹–åŠ¨
        MouseArea {
            anchors.fill: parent
            property point clickPos: Qt.point(0, 0)
            acceptedButtons: Qt.LeftButton | Qt.RightButton

            onPressed: (mouse) => {
                           if (mouse.button === Qt.RightButton) {
                               contextMenu.popup(mouse.scenePosition)
                               mouse.accepted = true
                               return
                           }
                           clickPos = Qt.point(mouse.x, mouse.y)
                       }

            onPositionChanged: (mouse) => {
                                   if (!(mouse.buttons & Qt.LeftButton))
                                   return
                                   var delta = Qt.point(mouse.x - clickPos.x, mouse.y - clickPos.y)
                                   window.x += delta.x
                                   window.y += delta.y
                               }

            onDoubleClicked: Qt.quit()
        }

        // 60ä¸ªåˆ»åº¦
        Repeater {
            model: 60
            Item {
                width: clock.width
                height: clock.height
                rotation: index * 6

                Rectangle {
                    property bool isHourMark: index % 5 === 0
                    width: isHourMark ? clock.baseHourMarkWidth : clock.baseHourMarkWidth / 4
                    height: isHourMark ? clock.baseHourMarkHeight : clock.baseHourMarkHeight / 2
                    color: isHourMark ? "#333" : "#999"
                    anchors.horizontalCenter: parent.horizontalCenter
                    anchors.top: parent.top
                    anchors.topMargin: isHourMark ? clock.baseHourMarkTopMargin : clock.baseHourMarkTopMargin + dp(5)
                    antialiasing: true
                }
            }
        }

        // æ—¶é’Ÿæ•°å­— 1-12
        Repeater {
            model: 12
            Item {
                width: clock.width
                height: clock.height
                rotation: (index + 1) * 30

                Text {
                    text: index + 1
                    font.pixelSize: clock.height/20
                    font.bold: true
                    color: "#333"
                    anchors.horizontalCenter: parent.horizontalCenter
                    anchors.top: parent.top
                    anchors.topMargin: clock.baseHourMarkTopMargin + clock.baseHourMarkHeight + dp(5)
                    rotation: -(index + 1) * 30
                }
            }
        }

        // æ—¥æœŸå’Œæ˜ŸæœŸæ˜¾ç¤º
        Column {
            anchors.centerIn: parent
            anchors.verticalCenterOffset: dp(hourHand.rotation > 90 && hourHand.rotation < 270 ? -40 : 40)
            spacing: dp(2)
            z: 5

            Text {
                id: dateText
                text: ""
                font.pixelSize: clock.height/20
                font.bold: true
                color: "#333"
                anchors.horizontalCenter: parent.horizontalCenter
            }

            Text {
                id: weekText
                text: ""
                font.pixelSize: dateText.font.pixelSize * 0.8
                color: "#666"
                anchors.horizontalCenter: parent.horizontalCenter
            }
        }

        // ä¸­å¿ƒåœ†ç‚¹
        Rectangle {
            width: hourHand.width * 1.5
            height: width
            radius: width / 2
            color: "#d32f2f"
            anchors.centerIn: parent
            z: 10
            antialiasing: true
        }

        // æ—¶é’ˆ
        Rectangle {
            id: hourHand
            width: minuteHand.width * 2
            height: minuteHand.height * 0.7
            color: "#333"
            radius: width/2
            anchors.horizontalCenter: parent.horizontalCenter
            anchors.verticalCenter: parent.verticalCenter
            anchors.verticalCenterOffset: -height / 2
            transformOrigin: Item.Bottom
            rotation: 0
            antialiasing: true
        }

        // åˆ†é’ˆ
        Rectangle {
            id: minuteHand
            width: secondHand.width * 1.5
            height: secondHand.height * 0.8
            color: "#555"
            radius: width/2
            anchors.horizontalCenter: parent.horizontalCenter
            anchors.verticalCenter: parent.verticalCenter
            anchors.verticalCenterOffset: -height / 2
            transformOrigin: Item.Bottom
            rotation: 0
            antialiasing: true
        }

        // ç§’é’ˆ
        Rectangle {
            id: secondHand
            width: dp(clock.baseHourMarkWidth / 2)
            height: clock.height/2 * 0.9
            color: "#d32f2f"
            radius: width/2
            anchors.horizontalCenter: parent.horizontalCenter
            anchors.verticalCenter: parent.verticalCenter
            anchors.verticalCenterOffset: -height / 2
            transformOrigin: Item.Bottom
            rotation: 0
            antialiasing: true
        }
        // å®šæ—¶å™¨æ›´æ–°æ—¶é—´
        Timer {
            id: timer
            interval: 16
            running: true
            repeat: true
            triggeredOnStart: true
            property int _currentDay: -1

            onTriggered: {
                var date = new Date()
                var hours = date.getHours()
                var minutes = date.getMinutes()
                var seconds = date.getSeconds()
                secondHand.rotation = seconds * 6
                minuteHand.rotation = minutes * 6 + seconds * 0.1
                hourHand.rotation = (hours % 12) * 30 + minutes * 0.5
                var year = date.getFullYear()
                var month = date.getMonth() + 1
                var day = date.getDate()
                if (_currentDay != day) {
                    clock.updateLunar(date)
                    _currentDay = day
                }
            }
        }

        function updateLunar(date) {
            var y = date.getFullYear();
            var m = date.getMonth() + 1;
            var d = date.getDate();
            var res = Lunar.calendar.solar2lunar(y, m, d);

            dateText.text = y + "å¹´" + m + "æœˆ" + d + "æ—¥";
            console.log(dateText.text)
            var weekInfo = res.ncWeek + "  " + res.IMonthCn + res.IDayCn;
            if (res.lunarFestival)      weekInfo += "ã€Œ" + res.lunarFestival + "ã€";
            else if (res.Term)          weekInfo += "ã€Œ" + res.Term + "ã€";
            weekText.text = weekInfo;
        }
    }
}
